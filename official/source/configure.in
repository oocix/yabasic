
dnl  Process this file with 'autoconf' to get a configure-script

dnl --- yabasic.h is one of the sources ---
AC_INIT(yabasic.h)
AM_CONFIG_HEADER(config.h)

dnl --- get canonical system name ---
AC_CANONICAL_HOST

dnl --- initialize automake
AM_INIT_AUTOMAKE(yabasic, 2.763)

dnl --- find out, which c-compiler is available ---
AC_PROG_CC

dnl --- set options appropriate for compiler ---
AC_CHECK_PROG(CCOPTIONS,gcc,-Wall,)

dnl --- check for X-Window system ---
AC_PATH_XTRA

dnl --- check for headers ---
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h limits.h stddef.h stdlib.h stdio.h float.h\
                 math.h time.h sys/time.h string.h strings.h\
                 unistd.h signal.h ctype.h malloc.h)

dnl --- check for typedefs, structures, and compiler characteristics ---
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl --- check for library functions ---
AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_STRTOD
AC_FUNC_SETPGRP
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_STAT
AC_CHECK_FUNCS([floor pow select sqrt strchr strerror strpbrk strrchr strstr mkstemp])

dnl --- check for header files ---
AC_CHECK_HEADER(string.h,AC_DEFINE(HAVE_STRING_HEADER,1,[defined, if string.h is present]))
AC_CHECK_HEADER(strings.h,AC_DEFINE(HAVE_STRINGS_HEADER,1,[defined, if strings.h is present]))

dnl --- check for curses.h
AC_CHECK_HEADER(ncurses.h,AC_DEFINE(HAVE_NCURSES_HEADER,1,[defined, if ncurses.h is present]))
AC_CHECK_HEADER(curses.h,AC_DEFINE(HAVE_CURSES_HEADER,1,[defined, if ncurses.h is present]))

dnl --- check if curses is available ---
AC_CHECK_LIB(ncurses,initscr)
AC_CHECK_LIB(curses,initscr)

dnl --- check for specific functions within ncurses
AC_CHECK_FUNCS(getnstr)

dnl --- check for specific functions ---
AC_CHECK_FUNCS(setitimer)
AC_CHECK_FUNCS(difftime)

dnl --- check for <alloca.h>
AC_FUNC_ALLOCA

dnl --- architecture of build machine ---
AC_DEFINE_UNQUOTED(UNIX_ARCHITECTURE,"$host",[architecture of build machine])

dnl --- build-time, that will be displayed in banner ---
AC_DEFINE_UNQUOTED(BUILD_TIME,"`date`",[build-time, that will be displayed in banner])

dnl --- translate makefile ---
AC_OUTPUT(Makefile)


